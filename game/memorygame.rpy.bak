## Layar MiniGames - Memory Card ###################################################
init python:

    # PENGATURAN GAME DEFAULT:

    # set standar jenis kartu
    all_cards = ['A', 'B', 'C']
    # lebar dan tinggi lapangan
    ww = 3
    hh = 3
    # berapa banyak kartu yang bisa dibuka dalam 1 putaran
    max_c = 2
    # ukuran teks dalam kartu untuk mode teks
    card_size = 48
    # waktu yang diberikan untuk lulus
    max_time = 25
    # jeda sebelum kartu menghilang
    wait = 0.5
    # mode kartu dengan gambar, bukan teks
    img_mode = True

    values_list = []
    temp = []
    # mengumumkan kartu gambar
    # harus dalam bentuk "images/card_*.png"
    # diperlukan "card_back.png" dan "card_empty.png"
    for fn in renpy.list_files():
        if fn.startswith("images/card_") and fn.endswith((".png")):
            name = fn[12:-4]
            renpy.image("card " + name, fn)
            if name != "empty" and name != "back":
                temp.append(str(name))
    # jika gambar ditemukan > 1,
    # lalu kami mengubah set jenis kartu, tetapi nama file
    if len(temp) > 1:
        all_cards = temp
    else:
        # jika tidak aktifkan mode teks,
        # karena gambarnya sangat sedikit.
        img_mode = False

    # fungsi inisialisasi bidang permainan
    def cards_init():
        global values_list
        values_list = []
        while len(values_list) + max_c <= ww * hh:
            current_card = renpy.random.choice(all_cards)
            for i in range(0, max_c):
                values_list.append(current_card)
        renpy.random.shuffle(values_list)
        while len(values_list) < ww * hh:
            values_list.append('empty')

# layar permainan
screen memo_scr:
    # timer
    timer 1.0 action If (memo_timer > 1, SetVariable("memo_timer", memo_timer - 1), Jump("memo_game_lose") ) repeat True
    # Layar
    grid ww hh:
        align (.5, .5) # berada di tengah
        for card in cards_list:
            button:
                left_padding 0
                right_padding 0
                top_padding 0
                bottom_padding 0
                background None
                if card["c_value"] == 'empty':
                    if img_mode:
                        add "card empty"
                    else:
                        text " " size card_size
                else:
                    if card["c_chosen"]:
                        if img_mode:
                            add "card " + card["c_value"]
                        else:
                            text card["c_value"] size card_size
                    else:
                        if img_mode:
                            add "card back"
                        else:
                            text "#" size card_size
                # klik tombol kartu
                action If ( (card["c_chosen"] or not can_click), None, [SetDict(cards_list[card["c_number"]], "c_chosen", True), Return(card["c_number"]) ] )
    text str(memo_timer) xalign .5 yalign 0.0 size card_size

# permainan itu sendiri
label memoria_game:
    $ cards_init()
    $ cards_list = []
    python:
        for i in range (0, len(values_list) ):
            if values_list[i] == 'empty':
                cards_list.append ( {"c_number":i, "c_value": values_list[i], "c_chosen":True} )
            else:
                cards_list.append ( {"c_number":i, "c_value": values_list[i], "c_chosen":False} )
    $ memo_timer = max_time
    # tampilkan layar permainan
    show screen memo_scr
    # lingkaran permainan utama
    label memo_game_loop:
        $ can_click = True
        $ turned_cards_numbers = []
        $ turned_cards_values = []
        $ turns_left = max_c
        label turns_loop:
            if turns_left > 0:
                $ result = ui.interact()
                $ memo_timer = memo_timer
                $ turned_cards_numbers.append (cards_list[result]["c_number"])
                $ turned_cards_values.append (cards_list[result]["c_value"])
                $ turns_left -= 1
                jump turns_loop
        # mencegah pembukaan kartu tambahan
        $ can_click = False
        if turned_cards_values.count(turned_cards_values[0]) != len(turned_cards_values):
            $ renpy.pause (wait, hard = True)
            python:
                for i in range (0, len(turned_cards_numbers) ):
                    cards_list[turned_cards_numbers[i]]["c_chosen"] = False
        else:
            $ renpy.pause (wait, hard = True)
            python:
                for i in range (0, len(turned_cards_numbers) ):
                    cards_list[turned_cards_numbers[i]]["c_value"] = 'empty'
                for j in cards_list:
                    if j["c_chosen"] == False:
                        renpy.jump ("memo_game_loop")
                renpy.jump ("memo_game_win")
        jump memo_game_loop

# Kalah
label memo_game_lose:
    hide screen memo_scr
    $ renpy.pause (0.1, hard = True)
    centered "{size=36}Coba Lagi!{/size}"
    jump memoria_game

# Menang
label memo_game_win:
    hide screen memo_scr
    $ renpy.pause (0.1, hard = True)
    centered "{size=36}{b}Menang!{/b}{/size}"
    jump jogjaplay2

image bg blck = "images/memory-bg.png"
# Label Start
label minigame_memory:
    scene bg blck
    $ max_time = 60
    $ ww, hh = 4, 4
    call memoria_game
    return
